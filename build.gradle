plugins {
    id 'java-library'
    id 'scala'
    id "com.github.maiflai.scalatest" version "0.32"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

group 'effiban'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.scalameta:scalameta_2.13:4.5.4'
}

testing {
    suites {
        test {
            dependencies {
                implementation 'org.scalatest:scalatest_2.13:3.2.12'
                implementation 'org.mockito:mockito-scala_2.13:1.17.5'
                runtimeOnly 'com.vladsch.flexmark:flexmark-all:0.64.0'
            }
            targets {
                all {
                    testTask.configure {
                        maxParallelForks = 1
                    }
                }
            }
        }
        integrationTest(JvmTestSuite) {
            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}
